@groups
	datetime d2l-input-datetime,d2l-input-datetime-labelless

== D2L Input DateTime ==

	@on closed
		@forEach [&datetime] as item
			${item}.d2l-input-time, ${item}.icon-button.clear.svg:
				absent

	@on open
		@forEach [&datetime] as item
			${item}.d2l-input-time, ${item}.icon-button.clear.svg:
				visible

			${item}.icon-button.clear.svg:
				inside ${item}

		d2l-input-datetime.d2l-input-time-label:
			visible

	d2l-input-datetime.d2l-input-date-label:
		visible

	d2l-input-datetime-labelless.d2l-input-date-label, d2l-input-datetime-labelless.d2l-input-time-label:
		absent

	@on mobile
		@on ltr
			d2l-input-datetime.d2l-input-date-label:
				left-of d2l-input-datetime.d2l-input-date.input-date

		@on rtl
			d2l-input-datetime.d2l-input-date-label:
				right-of d2l-input-datetime.d2l-input-date.input-date

		@on open
			@on ltr
				d2l-input-datetime.d2l-input-time-label:
					left-of d2l-input-datetime.d2l-input-time

			@on rtl
				d2l-input-datetime.d2l-input-time-label:
					right-of d2l-input-datetime.d2l-input-time

			@forEach [&datetime] as item
				${item}.d2l-input-time:
					below ${item}.d2l-input-date.input-date 20px

				@on ltr
					${item}.d2l-input-time:
						aligned vertically left ${item}.d2l-input-date.input-date

					${item}.icon-button.clear.svg:
						right-of ${item}.d2l-input-date.input-date ~30px

				@on rtl
					${item}.d2l-input-time:
						aligned vertically right ${item}.d2l-input-date.input-date

					${item}.icon-button.clear.svg:
						left-of ${item}.d2l-input-date.input-date ~30px

	@on desktop
		@on ltr
			d2l-input-datetime.d2l-input-date-label:
				aligned vertically left d2l-input-datetime.d2l-input-date.input-date

		@on rtl
			d2l-input-datetime.d2l-input-date-label:
				aligned vertically right d2l-input-datetime.d2l-input-date.input-date

		@on open
			@on ltr
				d2l-input-datetime.d2l-input-time-label:
					aligned vertically left d2l-input-datetime.d2l-input-time

			@on rtl
				d2l-input-datetime.d2l-input-time-label:
					aligned vertically right d2l-input-datetime.d2l-input-time

			@forEach [&datetime] as item
				${item}.d2l-input-time:
					aligned horizontally top ${item}.d2l-input-date.input-date

				@on ltr
					${item}.d2l-input-time:
						right-of ${item}.d2l-input-date.input-date 20px

					${item}.icon-button.clear.svg:
						right-of ${item}.d2l-input-time ~30px

				@on rtl
					${item}.d2l-input-time:
						left-of ${item}.d2l-input-date.input-date 20px

					${item}.icon-button.clear.svg:
						left-of ${item}.d2l-input-time ~30px
